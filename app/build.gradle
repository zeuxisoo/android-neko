apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "im.after.app"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode 5
        versionName "2.0.0"
    }
    signingConfigs {
        debug {
            def signFile     = file("./signing-configs.properties")
            def signingProps = new Properties()

            signingProps.load(new FileInputStream(signFile))

            storeFile file("../KeyStore.jks")
            storePassword signingProps['store_password']
            keyAlias signingProps['key_alias']
            keyPassword signingProps['key_password']
        }
        release {
            def signFile     = file("./signing-configs.properties")
            def signingProps = new Properties()

            signingProps.load(new FileInputStream(signFile))

            storeFile file("../KeyStore.jks")
            storePassword signingProps['store_password']
            keyAlias signingProps['key_alias']
            keyPassword signingProps['key_password']
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release

            zipAlignEnabled true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        applicationVariants.all { variant ->
            appendVersionNameVersionCode(variant, defaultConfig)
        }
    }
    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    compileOptions {
        incremental = false

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def appendVersionNameVersionCode(variant, defaultConfig) {
    def apk = variant.outputs[0].outputFile;
    def newName = "";

    newName = apk.name.replace(project.name, project.name + "-v" + android.defaultConfig.versionName);
    newName = newName.replace("-" + variant.buildType.name, "");
    newName = newName.replace(".apk", "-" + variant.buildType.name.toLowerCase() + ".apk");

    variant.outputs[0].outputFile = new File(apk.parentFile, newName);
    if (variant.outputs[0].zipAlign) {
        variant.outputs[0].zipAlign.outputFile = new File(apk.parentFile, newName.replace("-unaligned", ""));
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support:design:23.4.0'
    compile 'com.android.support:cardview-v7:23.4.0'

    // Dagger 2
    apt 'com.google.dagger:dagger-compiler:2.2'
    compile 'com.google.dagger:dagger:2.2'
    provided 'javax.annotation:jsr250-api:1.0'

    // Butter Knife
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'com.jakewharton:butterknife:8.0.1'

    // OkHttp & Retrofit (with converter factory, call adapter)
    compile 'com.squareup.okhttp3:okhttp:3.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.0'

    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    // RxAndroid & RxJava
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'io.reactivex:rxjava:1.1.5'

    // DBFlow
    apt "com.github.Raizlabs.DBFlow:dbflow-processor:3.0.1"
    compile "com.github.Raizlabs.DBFlow:dbflow-core:3.0.1"
    compile "com.github.Raizlabs.DBFlow:dbflow:3.0.1"

    // Logger
    compile "com.github.orhanobut:logger:1.12"

    // Smooth Progress Bar & Circular Progress Bar
    compile 'com.github.castorflex.smoothprogressbar:library:1.1.0'
    compile "com.github.castorflex.smoothprogressbar:library-circular:1.2.0"

    // Super Recycler View
    compile 'com.malinskiy:superrecyclerview:1.1.4'

    // Recycler View Animators
    compile 'jp.wasabeef:recyclerview-animators:2.2.3'

    // Material dialogs
    compile 'com.afollestad.material-dialogs:core:0.8.5.9'
}
